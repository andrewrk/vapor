const from_base64 = atob or alert "this browser can't decode base64 data"

const flipped_horizontally_flag = 0x80000000
const flipped_vertically_flag   = 0x40000000
const flipped_diagonally_flag   = 0x20000000
const imageid_mask              = 0x1fffffff

window.Map = class Map
  ~>
    @tilesets = []
    @layers = []
  tile: (layer, x, y) -> @layers[layer][y]?[x] ? 0
  draw_tile: (context, x, y) ->
    tile = @tile 0, x, y
    return if tile is 0
    imageid = tile & imageid_mask
    for tileset of @tilesets
      image_bounds = tileset.image_bounds imageid
      break if image_bounds?
    context.drawImage(tileset.image
      image_bounds.x, image_bounds.y, image_bounds.width, image_bounds.height
      x * @scale,     y * @scale,     image_bounds.width, image_bounds.height
    )

Map.Tileset = class Tileset
  image_bounds: (imageid) ->
    local_id = imageid - @first_gid
    return null if local_id < 0
    column_count = @width / @tilewidth
    row = Math.floor local_id / column_count
    row_count = @height / @tileheight
    return null if row >= row_count
    column = local_id % column_count
    return {} =
      x: column * @tilewidth
      y: row * @tileheight
      width: @tilewidth
      height: @tileheight


Map.parse = (text, create_wait_condition) ->
  map = new Map!
  xml = new DOMParser!.parseFromString text, "text/xml"
  root = xml.getElementsByTagName("map")[0]
  map.scale = parseInt root.getAttribute "tilewidth"
  for tileset_element of root.getElementsByTagName "tileset"
    tileset = new Tileset!
    tileset.first_gid = parseInt tileset_element.getAttribute "firstgid"
    tileset.tilewidth = parseInt tileset_element.getAttribute "tilewidth"
    tileset.tileheight = parseInt tileset_element.getAttribute "tileheight"
    image_element = tileset_element.getElementsByTagName("image")[0]
    tileset.image = new Image!
    tileset.image.src = image_element.getAttribute "source"
    tileset.image.onload = create_wait_condition "tileset"
    tileset.width = parseInt image_element.getAttribute "width"
    tileset.height = parseInt image_element.getAttribute "height"
    map.tilesets.push tileset
  for layer_element of root.getElementsByTagName "layer"
    width  = parseInt layer_element.getAttribute "width"
    height = parseInt layer_element.getAttribute "height"
    data_element = layer_element.getElementsByTagName("data")[0]
    if data_element.getAttribute("compression")
      alert "can't decompress map. store maps with base64 uncompressed data."
      return
    data_string = from_base64 data_element.textContent.trim!
    i = 0
    map.layers.push layer = []
    for y from 0 til height
      layer.push row = []
      for x from 0 til width
        tile_value  = data_string.charCodeAt(i++)
        tile_value |= data_string.charCodeAt(i++) << 8
        tile_value |= data_string.charCodeAt(i++) << 16
        tile_value |= data_string.charCodeAt(i++) << 24
        row.push tile_value
  map
