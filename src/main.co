#depend "chem"
#depend "box2d" bare

Chem.onReady ->
  const canvas = document.getElementById "game"
  const engine = new Chem.Engine canvas
  const batch = new Chem.Batch!
  const boom = new Chem.Sound 'sfx/boom.ogg'
  const ship = new Chem.Sprite 'ship',
    batch: batch
    pos: new Chem.Vec2d(200, 200)
    rotation: Math.PI / 2
  rotation_speed = Math.PI * 0.04
  thrust_amt = 0.1

  # GIMME THE FIZZAQS!
  world = null
  ship_body = null
  let
    world_box = new b2AABB!
    world_box.minVertex.Set 0, 0
    world_box.maxVertex.Set parseInt(canvas.getAttribute "width"), parseInt(canvas.getAttribute "height")
    world := new b2World world_box, new b2Vec2(0, 300), true
    circle = new b2CircleDef!
    circle.density = 1.0
    circle.radius = 20
    circle.restitution = 1.0
    circle.friction = 0
    ship_body_def = new b2BodyDef!
    ship_body_def.AddShape circle
    ship_body_def.position.Set ship.pos.x, ship.pos.y
    ship_body := world.CreateBody ship_body_def

  engine.on 'update', (dt, dx) ->
    world.Step dt, 1

    ship.pos.x = ship_body.m_position.x
    ship.pos.y = ship_body.m_position.y

    # rotate the ship with left and right arrow keys
    if engine.buttonState Chem.Button.Key_Left
      ship.rotation -= rotation_speed * dx
    if engine.buttonState Chem.Button.Key_Right
      ship.rotation += rotation_speed * dx

    # press space to blow yourself up
    if engine.buttonJustPressed Chem.Button.Key_Space
      boom.play!
      ship.setAnimationName 'boom'
      ship.setFrameIndex 0
      ship.on 'animation_end', ->
        ship.delete!

  engine.on 'draw', (context) ->
    # clear canvas to black
    context.fillStyle = '#000000'
    context.fillRect 0, 0, engine.size.x, engine.size.y

    # draw all sprites in batch
    engine.draw batch

    # draw a little fps counter in the corner
    context.fillStyle = '#ffffff'
    engine.drawFps!

  engine.start!
  canvas.focus!

